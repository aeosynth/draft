Player = require \./player
genHash = require \./generate/hash

class Human extends Player
  (@sock, @isHost) ->
    super!
    @<<<< sock{ id, name, zone }
    @<<<<
      cards: []
    @attach sock

  dropped: false

  attach: (sock) ->
    # TODO auto reconnect, d/c notice
    @sock.removeAllListeners!
    @sock = sock
    @send = sock~send
    @send \set { @isHost, pack: @packs.0, (@zone): @cards }

    # TODO events hash
    # TODO bubbling, single arg w/ data + src, namespaces
    sock.on \pick @~pick
    sock.on \name @~setName
    sock.on \drop ~> @emit \drop @
    sock.on \hash (deck) ~>
      @hash = genHash deck
      @emit \meta
    sock.on \zone (zone) ~>
      return unless zone of <[ main side ]>
      @zone = zone

    if @isHost
      sock.on \start ~> @emit \start it

  setName: ->
    return unless typeof it is \string
    @name = it.slice 0 16
    @emit \meta

  sendPack: ->
    pack = @packs.0

    @time = 20 + pack.length * 5

    if @dropped or pack.length is 1
      process.nextTick @~autopick
    else
      @send \set { pack }

  drop: ->
    @dropped = true
    @sock.removeAllListeners!
    @send \error 'you were dropped'
    @autopick!

  pick: (index, autopick) ->
    return unless typeof index is \number
    return unless super index, autopick
    @send \add @[@zone][* - 1]

module.exports = Human
