{EventEmitter} = require \events

class Sock extends EventEmitter
  (@ws) ->
    {id, name, room, zone} = ws.request._query
    name ?.= slice 0 16
    unless zone of <[ main side ]>
      zone = \main
    @ <<<< { id, name, room, zone }
    ws.on \message @~message
    ws.on \close ~> @emit \drop

  message: ->
    try
      msg = JSON.parse it
    catch
      console.log 'error parsing message' it
      return
    @emit ...msg

  send: (name, args) ->
    @ws.send JSON.stringify { name, args }

module.exports = Sock
