request = require \request
cheerio = require \cheerio

getCards = (setName, cb) ->
  url = "http://gatherer.wizards.com/Pages/Search/Default.aspx
    ?output=spoiler&set=[%22#setName%22]"
  request url, (err, res, html) ->
    throw err if err
    cb parse html, setName

parse = (html, setName) ->
  var card
  cards = []
  split = []
  rarityRE = new RegExp do
    "(?:^|, )#setName (Land|Common|Uncommon|Rare|Mythic Rare|Special)"

  $ = cheerio.load html

  $ '.textspoiler tr' .each ->
    cells = @children!

    $val = $ cells.1
    val = $val.text!trim!

    return unless val

    key = $ cells.0 .text!trim!replace \: '' .toLowerCase!

    switch key
    case \name
      card :=
        cmc: 0
        id: $val.find \a .attr \href .match /\d+/ .0
        name: val.replace \Ã† \AE
      if ~val.indexOf \//
        split.push card
      else
        cards.push card
    case \cost
      card.cost = val
      cost = val.replace \X '' .replace /\(\w\/\w\)/g ->
        [_, left, right] = it.match /(\w)\/(\w)/
        if left is \2
          card.cmc += 1
          return right
        if right is \P
          return left
        return \Y

      [_, colorless, colored] = cost.match /(\d*)(\w*)/
      card.cmc += parseInt(colorless or 0) + colored.length
    case \color # eg pact of negation
      card.color = if val is \Blue
        \U
      else
        val.0
    case \set/rarity
      if match = val.match rarityRE
        rarity = match.1.toLowerCase!
        if rarity is 'mythic rare'
          rarity = \mythic
      else
        console.log "rarity not found: [#{card.name}] [#val]"
        rarity = \land
      card.rarity = rarity
    default
      card[key] = val

  joined = {}
  for card of split
    name = card.name.replace /\ \(.+/ ''

    half = joined[name] or=
      cmc: 0
      cost: ''
      id: card.id
      name: name
      rarity: card.rarity

    half.cmc  += card.cmc
    half.cost += card.cost

  for name, card in joined
    cards.push card

  for card of cards
    continue if card.color # eg pact of negation

    unless (cost = card.cost) and (colors = cost.match /R|G|B|U|W/g)
      card.color = \A # artifact
      continue

    card.color = if /^(.)\1*$/.test colors * ''
      colors.0
    else
      \Y # yellow

  cards

module.exports = getCards
